
/*
 * Copyright 2010 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import org.apache.tools.ant.filters.ReplaceTokens
import org.asciidoctor.gradle.AsciidoctorTask
import org.gradle.build.docs.Docbook2Xhtml
import org.gradle.build.docs.UserGuideTransformTask
import org.gradle.build.docs.dsl.docbook.AssembleDslDocTask
import org.gradle.build.docs.dsl.source.ExtractDslMetaDataTask
import org.gradle.build.docs.dsl.source.GenerateDefaultImportsTask
import org.gradle.build.docs.releasenotes.*
import org.gradle.build.docs.releasenotes.checks.*
import org.gradle.gradlebuild.BuildEnvironment
import org.gradle.gradlebuild.ProjectGroups
import org.gradle.gradlebuild.PublicApi
import org.gradle.gradlebuild.docs.PegDown
import org.gradle.gradlebuild.unittestandcompile.ModuleType

plugins {
    id 'base'
    id 'gradlebuild.jsoup'
    id 'javascript-base'
    id 'org.asciidoctor.convert'
}

repositories {
    javaScript.googleApis()
}

configurations {
    userGuideStyleSheets
    userGuideTask
    jquery
    jqueryTipTip

    cssElements {
        // define an outgoing configuration, aimed at consumers looking
        // for CSS resources
        attributes {
            attribute(Usage.USAGE_ATTRIBUTE, project.objects.named(Usage, 'css-resources'))
        }
        canBeResolved = false
        canBeConsumed = true
    }
}

dependencies {
    asciidoctor "org.gradle:docs-asciidoctor-extensions:0.2.0"
    asciidoctor project.files("src/main/resources")

    userGuideTask 'xalan:xalan:2.7.1', 'xerces:xercesImpl:2.11.0'
    userGuideTask 'xhtmlrenderer:xhtmlrenderer:R8rc1'
    userGuideTask 'itext:itext:2.0.8'
    userGuideTask 'xslthl:xslthl:2.0.1'

    userGuideStyleSheets 'docbook:docbook-xsl:1.75.2@zip'
    jquery "jquery:jquery.min:1.8.0@js"
    jqueryTipTip "com.drewwilson.code:jquery.tipTip:1.3:minified@js"

    testCompile "org.pegdown:pegdown:1.1.0"
    testCompile testLibraries.jsoup
    testCompile "org.gebish:geb-spock:0.9.3"
    testCompile('org.seleniumhq.selenium:selenium-htmlunit-driver:2.42.2')
    testCompile libraries.commons_httpclient.coordinates
    testCompile "org.apache.httpcomponents:httpmime:${libraries.commons_httpclient.version}"
    testCompile project(":core")
}

gradlebuildJava {
    moduleType = ModuleType.PLUGIN
}

ext {
    srcDocsDir = file('src/docs')
    releaseFeaturesFile = file("$srcDocsDir/release/release-features.txt")
    docsOutputDir = new File(buildDir, 'docs')
    userguideSrcDir = new File(srcDocsDir, 'userguide')
    userguideIntermediateOutputDir = new File(buildDir, 'userguideIntermediate')
    userguidePdfOutputDir = new File(buildDir, 'userguidePdf')
    userguideOutputDir = new File(docsOutputDir, 'userguide')
    distDocsDir = new File(buildDir, 'distDocs')
    srcSamplesDir = file('src/samples')
    samplesOutputDir = new File(buildDir, 'samples')
    docbookSrc = new File(project.buildDir, 'src')
    dslSrcDir = new File(srcDocsDir, 'dsl')
}

ext.outputs = [:]
outputs.samples = files(samplesOutputDir) {
    builtBy 'samples'
}
outputs.distDocs = files(distDocsDir) {
    builtBy 'distDocs'
}
outputs.docs = files(docsOutputDir) {
    builtBy 'javadocAll', 'userguide', 'dslHtml', 'releaseNotes'
}

tasks.withType(Docbook2Xhtml) {
    dependsOn userguideStyleSheets
    classpath = configurations.userGuideTask
    stylesheetsDir = userguideStyleSheets.destinationDir
}

tasks.withType(AssembleDslDocTask) {
    classDocbookDir = dslSrcDir
}

task configureCss {
    doLast {
        def tokens = fileTree(dir: "src/docs/css/images", include: "*.*").collectEntries {
            [it.name, it.bytes.encodeBase64().toString()]
        }

        css.inputs.property 'tokens', tokens
        css.filter ReplaceTokens, tokens: tokens
    }
}

task css(type: Sync, dependsOn: configureCss) {
    into "$buildDir/css"
    from "src/docs/css"
    include "*.css"
    include "*.svg"
}

artifacts {
    // declare that the generated css directory is an output of cssElements
    cssElements file: css.destinationDir, builtBy: css
}

// TODO This is a hack pending a solution to https://github.com/gradle/gradle/issues/4612
// Don't publish the CSS artifacts when calling uploadArchives
configurations.archives.artifacts.removeAll(configurations.cssElements.artifacts)

ext.cssFiles = fileTree(css.destinationDir) {
    builtBy css
}

def imageFiles = fileTree(userguideSrcDir) {
    include 'images/*.png'
    include 'images/*.gif'
    include 'images/*.jpg'
}
def resourceFiles = imageFiles + cssFiles

task samples(type: Copy) {
    from srcSamplesDir
    into samplesOutputDir

    exclude '**/*.out'
    exclude '**/*.sample.conf*'
    exclude '**/readme.xml'
    exclude '**/build/**'
    exclude '**/.gradle/**'
}

task userguideStyleSheets(type: Copy) {
    File stylesheetsDir = new File(srcDocsDir, 'stylesheets')
    into new File(buildDir, 'stylesheets')
    from(stylesheetsDir) {
        include '**/*.xml'
        include '*.xsl'
    }
    from(cssFiles)
    from({ zipTree(configurations.userGuideStyleSheets.singleFile) }) {
        // Remove the prefix
        eachFile { fcd -> fcd.path = fcd.path.replaceFirst('^docbook-xsl-[0-9\\.]+/', '') }
    }
}

def generatedResourcesDir = gradlebuildJava.generatedResourcesDir

task dslMetaData(type: ExtractDslMetaDataTask) {
    source { javadocAll.source }
    destFile = new File(docbookSrc, 'dsl-meta-data.bin')
}

task dslDocbook(type: AssembleDslDocTask, dependsOn: [dslMetaData]) {
    sources = fileTree(dir: dslSrcDir, includes: ['*.xml'])
    sourceFile = new File(dslSrcDir, 'dsl.xml')
    classMetaDataFile = dslMetaData.destFile
    pluginsMetaDataFile = new File(dslSrcDir, 'plugins.xml')
    destFile = new File(docbookSrc, 'dsl.xml')
    linksFile = new File(docbookSrc, 'api-links.bin')
}

task defaultImports(type: GenerateDefaultImportsTask, dependsOn: dslMetaData) {
    metaDataFile = dslMetaData.destFile
    importsDestFile = new File(generatedResourcesDir, "default-imports.txt")
    mappingDestFile = new File(generatedResourcesDir, "api-mapping.txt")
    // These are part of the API, but not the DSL
    excludePackage 'org.gradle.tooling.**'
    excludePackage 'org.gradle.testfixtures.**'

    // Tweak the imports due to some inconsistencies introduced before we automated the default-imports generation
    excludePackage 'org.gradle.plugins.ide.eclipse.model'
    excludePackage 'org.gradle.plugins.ide.idea.model'
    excludePackage 'org.gradle.api.tasks.testing.logging'

    // TODO - rename some incubating types to remove collisions and then remove these exclusions
    excludePackage 'org.gradle.plugins.binaries.model'

    // Exclude classes that were moved in a different package but the deprecated ones are not removed yet
    excludePackage 'org.gradle.platform.base.test'
}

task dslStandaloneDocbook(type: UserGuideTransformTask, dependsOn: [dslDocbook]) {
    sourceFile = dslDocbook.destFile
    destFile = new File(docbookSrc, 'dsl-standalone.xml')
    dsldocUrl = '.'
    linksFile = dslDocbook.linksFile
    javadocUrl = '../javadoc'
    dsldocUrl = '../dsl'
    websiteUrl = 'https://gradle.org'
}

task dslHtml(type: Docbook2Xhtml) {
    group = "Documentation"
    source dslStandaloneDocbook
    destDir = new File(docsOutputDir, 'dsl')
    stylesheetName = 'dslHtml.xsl'
    resources = cssFiles + fileTree(dslSrcDir) {
        include '*.js'
    }
    ext.entryPoint = "$destDir/index.html"
}

def javaApiUrl = "https://docs.oracle.com/javase/7/docs/api"
def groovyApiUrl = "http://docs.groovy-lang.org/docs/groovy-${libraries.groovy.version}/html/gapi"
def mavenApiUrl = "http://maven.apache.org/ref/${libraries.maven3.version}/maven-model/apidocs"

task javadocAll(type: Javadoc) {
    ext.stylesheetFile = file("src/docs/css/javadoc.css")
    inputs.file stylesheetFile withPropertyName "stylesheetFile" withPathSensitivity PathSensitivity.NAME_ONLY

    group = 'documentation'
    options.encoding = 'utf-8'
    options.docEncoding = 'utf-8'
    options.charSet = 'utf-8'
    if (BuildEnvironment.javaVersion.isJava8Compatible()) {
        options.addStringOption 'Xdoclint:syntax,html,reference', '-quiet'
    }
    options.addStringOption "stylesheetfile", stylesheetFile.absolutePath
    source ProjectGroups.INSTANCE.getPublicJavaProjects(project).collect { project -> project.sourceSets.main.allJava }
    destinationDir = new File(docsOutputDir, 'javadoc')
    classpath = files(ProjectGroups.INSTANCE.getPublicJavaProjects(project).collect {project -> [project.sourceSets.main.compileClasspath, project.sourceSets.main.output] })
    PublicApi.includes.each {
        include it
    }
    PublicApi.excludes.each {
        exclude it
    }
    options.links(javaApiUrl, groovyApiUrl, mavenApiUrl)
    title = "Gradle API $version"
    ext.entryPoint = "$destinationDir/index.html"
}

task checkstyleApi(type: Checkstyle) {
    source javadocAll.source
    configFile = new File(checkstyle.configDir, "checkstyle-api.xml")
    classpath = files()
    reports.xml.destination = file("$checkstyle.reportsDir/checkstyle-api.xml")
}

task distDocs(type: AsciidoctorTask) {
    sourceDir = userguideSrcDir
    sources { include 'getting-started.adoc' }
    outputDir = distDocsDir
    separateOutputDirs = false
    options doctype: 'book'
    backends 'html5'
}

asciidoctorj {
    noDefaultRepositories = true
    version = '1.5.7'
}

task userguideAsciidoc(type: AsciidoctorTask) {
    dependsOn css, samples, defaultImports
    sourceDir = userguideSrcDir
    sources {
        include '*.adoc'
        exclude 'javaProject*Layout.adoc'
    }
    outputDir = userguideIntermediateOutputDir
    // Verbose mode reports broken links. See asciidoctor/asciidoctor-gradle-plugin#233
    requires file('src/docs/ruby/verbose-mode.rb')
    separateOutputDirs = false
    options doctype: 'book'
    backends = ['html5']

    attributes stylesdir      : '../css/',
        stylesheet            : 'manual.css',
        imagesdir             : 'images',
        nofooter              : true,
        icons                 : null,
        'note-caption'        : '&#128161;',
        'tip-caption'         : '&#128161;',
        'important-caption'   : '&#10071;️',
        'caution-caption'     : '&#9888;',
        'warning-caption'     : '&#9888;',
        'source-highlighter'  : 'prettify',
        sectanchors           : true,
        sectlinks             : false,
        linkattrs             : true,
        reproducible          : '',
        lang                  : 'en-US',
        encoding              : 'utf-8',
        idprefix              : '',
        toc                   : 'auto',
        toclevels             : 1,
        'toc-title'           : 'Contents',
        'gradle-home-url'     : 'https://gradle.org',
        'java-api-url'        : 'https://docs.oracle.com/javase/7/docs/api',
        'ant-manual-url'      : 'https://ant.apache.org/manual',
        'guides-url'          : 'https://guides.gradle.org',
        'gradle-version'      : version,
        'groovy-dsl-path'     : "../dsl",
        'javadoc-path'        : "../javadoc",
        'samples-path'        : "../../samples"
}

task userguidePdf(type: AsciidoctorTask) {
    dependsOn defaultImports
    sourceDir = userguideSrcDir
    sources { include 'userguide_single.adoc' }
    outputDir = userguidePdfOutputDir
    separateOutputDirs = false
    options doctype: 'book'
    backends 'pdf'

    attributes \
        lang                : 'en-US',
        encoding            : 'utf-8',
        toc                 : 'preamble',
        toclevels           : 3,
        imagesdir           : 'images',
        'source-highlighter': 'coderay',
        'gradle-home-url'   : 'https://gradle.org',
        'java-api-url'      : 'https://docs.oracle.com/javase/7/docs/api',
        'ant-manual-url'    : 'https://ant.apache.org/manual',
        'guides-url'        : 'https://guides.gradle.org',
        'gradle-version'    : version,
        'groovy-dsl-path'   : "https://gradle.org/$version/dsl",
        'javadoc-path'      : "https://gradle.org/$version/javadoc",
        'samples-path'      : "../../samples"
}

// Avoid overlapping outputs by copying exactly what we want from other intermediate tasks
task userguide(type: Copy) {
    dependsOn userguideAsciidoc, userguidePdf
    description = 'Generates the userguide HTML and PDF'
    group = 'documentation'

    outputs.dir userguideOutputDir

    from resourceFiles
    from userguideIntermediateOutputDir
    from userguidePdfOutputDir

    into userguideOutputDir
    rename 'userguide_single.pdf', 'userguide.pdf'
}

task editReleaseNotes() {
    group = "release notes"
    doLast {
        Class.forName("java.awt.Desktop").newInstance().edit(file("src/docs/release/notes.md"))
    }
}

task releaseNotesMarkdown(type: PegDown, group: "release notes", constructorArgs: [file("src/docs/release/notes.md"), new File(buildDir, "release-notes-raw/release-notes.html")])

task releaseNotes(type: Copy) {
    group = "release notes"
    ext.fileName = "release-notes.html"
    into "$docsOutputDir"
    from releaseNotesMarkdown
    jsoup.plugins "src/transforms/release-notes.gradle"
    filter(ReplaceTokens, tokens: [version: project.version.toString(), baseVersion: rootProject.baseVersion])
    ext.entryPoint = file("$docsOutputDir/$fileName")
}

tasks.addRule("view«Doc Task Name» - Opens entry point") { String taskName ->
    if (taskName.startsWith("view")) {
        def realTaskName = (taskName - "view")
        realTaskName = realTaskName[0].toLowerCase() + realTaskName[1..-1]
        def task = tasks.findByName(realTaskName)
        if (task && task.hasProperty("entryPoint")) {
            tasks.create(taskName) {
                dependsOn task
                doLast {
                    Class.forName("java.awt.Desktop").newInstance().browse(file(task.entryPoint).toURI())
                }
            }
        }
    }
}

task copyReleaseFeatures(type: Copy) {
    from releaseFeaturesFile
    into generatedResourcesDir
}

sourceSets.main.output.dir generatedResourcesDir, builtBy: [defaultImports, copyReleaseFeatures]

test {
    dependsOn releaseNotes
    inputs.files releaseNotesMarkdown.markdownFile withPropertyName "releaseNotesSource" withPathSensitivity PathSensitivity.NONE
    inputs.dir releaseNotes.destinationDir withPropertyName "releaseNotesRendered" withPathSensitivity PathSensitivity.NONE
    inputs.property "systemProperties", [:]
    systemProperty "org.gradle.docs.releasenotes.source", releaseNotesMarkdown.markdownFile
    systemProperty "org.gradle.docs.releasenotes.rendered", new File(releaseNotes.destinationDir, releaseNotes.fileName)
    systemProperty "org.gradle.docs.releasefeatures", releaseFeaturesFile
}

task docs {
    dependsOn javadocAll, userguide, distDocs, dslHtml, releaseNotes
    description = 'Generates all documentation'
    group = 'documentation'
}

task docsZip(type: Zip) {
    from project.outputs.docs
}
